plugins {
    id 'java'
    alias(libs.plugins.springBoot) apply(true)
    alias(libs.plugins.openapi) apply(true)
}


group = 'io.github.agomezlucena'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

configurations {
    mockitoAgent
}

dependencies {
    implementation platform(libs.springBootBom)
    implementation "org.springframework.boot:spring-boot-starter"
    implementation "org.springframework.boot:spring-boot-starter-web"
    implementation "org.springframework.boot:spring-boot-starter-jdbc"
    implementation libs.mybatisStarter
    implementation "org.springdoc:springdoc-openapi-starter-webmvc-ui:2.3.0"
    implementation 'org.liquibase:liquibase-core'
    implementation 'org.openapitools:jackson-databind-nullable:0.2.6'
    implementation 'org.postgresql:postgresql'
    implementation project(":libtory-books")
    implementation project(":libtory-shared")

    testRuntimeOnly "org.junit.platform:junit-platform-launcher"
    testImplementation 'org.testcontainers:junit-jupiter'
    testImplementation "org.springframework.boot:spring-boot-starter-test"
    testImplementation 'org.springframework.boot:spring-boot-testcontainers'
    testImplementation 'org.liquibase:liquibase-core'
    testImplementation 'org.testcontainers:postgresql'
    testImplementation libs.restAssureMockMvc
    testImplementation (libs.mockito)
    mockitoAgent(libs.mockito) {
        transitive = false
    }
}

test {
    useJUnitPlatform()
    jvmArgs += "-javaagent:${configurations.mockitoAgent.asPath}"
}

sourceSets.main.java.srcDirs += "${layout.buildDirectory.get()}/generated/src/main/java"
compileJava.dependsOn tasks.openApiGenerate

bootJar {
    enabled=true
}

openApiValidate {
    inputSpec.set("${projectDir}/src/main/resources/libtory-rest-api.yaml")
}

openApiGenerate {
    generatorName = "spring"
    inputSpec = "${projectDir}/src/main/resources/libtory-rest-api.yaml"
    outputDir = "${layout.buildDirectory.get()}/generated"
    apiPackage = "io.github.agomezlucena.libtory.rest.api"
    modelPackage = "io.github.agomezlucena.libtory.rest.model"
    configOptions = [
            interfaceOnly: "true",
            useSpringBoot3: "true",
            java8: "true",
            hideGenerationTimestamp: "true"
    ]
}